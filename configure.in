dnl Configure script for GNU Mach.
dnl Copyright 1997, 1998, 1999 Free Software Foundation, Inc.

dnl Permission to use, copy, modify and distribute this software and its
dnl documentation is hereby granted, provided that both the copyright
dnl notice and this permission notice appear in all copies of the
dnl software, derivative works or modified versions, and any portions
dnl thereof, and that both notices appear in supporting documentation.
dnl
dnl THE FREE SOFTWARE FOUNDATION ALLOWS FREE USE OF THIS SOFTWARE IN ITS
dnl "AS IS" CONDITION.  THE FREE SOFTWARE FOUNDATION DISCLAIMS ANY
dnl LIABILITY OF ANY KIND FOR ANY DAMAGES WHATSOEVER RESULTING FROM THE
dnl USE OF THIS SOFTWARE.


AC_INIT(kern/ipc_kobject.c)
AC_PREREQ(2.12)

#
# Deduce output var `systype' from configuration parms.
#
AC_CANONICAL_HOST

case "$host_cpu" in
i[[3456]]86) systype=i386 ;;
*) AC_MSG_ERROR([unsupported CPU type]) ;;
esac

case "$host_os" in
gnu*) ;;
*) AC_MSG_ERROR([sorry, this is the gnu os, not $host_os]) ;;
esac

AC_SUBST(systype)
AC_SUBST(cross_compiling)

# Default prefix is / for the kernel.
AC_PREFIX_DEFAULT()

#
# Options
#
AC_ARG_ENABLE(kdb,
[  --enable-kdb             enable use of in-kernel debugger],
[test "x$enableval" = xno || AC_DEFINE(MACH_KDB)])

AC_ARG_ENABLE(kmsg,
[  --enable-kmsg            enable use of kmsg device],
[test "x$enableval" = xno || AC_DEFINE(MACH_KMSG)])

#
# Programs
#

AC_PROG_INSTALL
AC_PROG_AWK

AC_CHECK_TOOL(CC, gcc)
# That check handles cross-compilation well, but AC_PROG_CC tests for GCC
# and sets default CFLAGS nicely for us, so do that too.
AC_PROG_CC_LOCAL

AC_CHECK_TOOL(LD, ld)
AC_CHECK_TOOL(NM, nm)

AC_CHECK_TOOL(MIG, mig, mig)
AC_CHECK_TOOL(MAKE, make, make)

# Set up `machine' link in build directory for easier header file location.
AC_LINK_FILES(${systype}/${systype},machine)

# Do machine-specific configuration last so that it can override anything
# set above if necessary.

AC_CONFIG_SUBDIRS(linux ${systype})

AC_OUTPUT(Makefile)
