dnl Configure script for GNU Mach.
dnl Copyright 1997, 1998, 1999, 2004 Free Software Foundation, Inc.

dnl Permission to use, copy, modify and distribute this software and its
dnl documentation is hereby granted, provided that both the copyright
dnl notice and this permission notice appear in all copies of the
dnl software, derivative works or modified versions, and any portions
dnl thereof, and that both notices appear in supporting documentation.
dnl
dnl THE FREE SOFTWARE FOUNDATION ALLOWS FREE USE OF THIS SOFTWARE IN ITS
dnl "AS IS" CONDITION.  THE FREE SOFTWARE FOUNDATION DISCLAIMS ANY
dnl LIABILITY OF ANY KIND FOR ANY DAMAGES WHATSOEVER RESULTING FROM THE
dnl USE OF THIS SOFTWARE.

m4_include([version.m4])

AC_INIT([PACKAGE], [VERSION], [bug-hurd@gnu.org], [gnumach])
AC_CONFIG_SRCDIR([kern/ipc_kobject.c])
AC_PREREQ(2.57)

#
# Deduce output var `systype' from configuration parms.
#
AC_CANONICAL_HOST

case "$host_cpu" in
i[[3456]]86) systype=i386 ;;
*) AC_MSG_ERROR([unsupported CPU type]) ;;
esac

AC_SUBST([systype])
AC_SUBST([cross_compiling])

# Default prefix is / for the kernel.
AC_PREFIX_DEFAULT([])

#
# Options
#
AC_ARG_ENABLE([kdb],
AS_HELP_STRING([--enable-kdb], [enable use of in-kernel debugger]),
[test "x$enableval" = xno || AC_DEFINE([MACH_KDB], [1], [enable use of in-kernel debugger])])

AC_ARG_ENABLE([kmsg],
AS_HELP_STRING([--enable-kmsg], [enable use of kmsg device [[default=yes]]]),
[test "x$enableval" = xno || AC_DEFINE([MACH_KMSG], [], [enable use of kmsg device])],
			    [AC_DEFINE([MACH_KMSG], [], [enable use of kmsg device])])

#
# Programs
#

AC_PROG_INSTALL
AC_PROG_AWK
AC_PROG_CC

AC_CHECK_TOOL([LD], [ld])
AC_SUBST([LDFLAGS])
AC_CHECK_TOOL([NM], [nm])

AC_CHECK_TOOL([MIG], [mig], [mig])

# Set up `machine' link in build directory for easier header file location.
AC_CONFIG_LINKS([machine:${systype}/${systype}])

AC_CONFIG_SUBDIRS([linux])

# Do machine-specific configuration last so that it can override anything
# set above if necessary.
if test "$systype" = i386; then
  AC_CONFIG_SUBDIRS([i386])
fi

AC_CONFIG_FILES([Makefile version.c doc/Makefile])
AC_OUTPUT
